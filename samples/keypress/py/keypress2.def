%{
from inthsm import HHAccept, HHAbort

import sys

def getch(pd):
    print('\nEnter [a - h], or [x|X] to exit/abort...\n> ', end='', flush=True);
    while True:
       ch = sys.stdin.read(1)
       if ch != '\n':
           return ch

class MyData:
    test = 0

%}

%prefix k2_

%manual_event_id
%manual_state_id

%event	a b c d e f g h
	x X

%state	s0
	s1
	s11
	s2
	s21
	s211

%hiera	s0 (s1 (s11),
	    s2 (s21 (s211)))

%start	s0

%%

s0: . -> s1
    e -> s211
    x --	{ HHAccept() }
    X --	{ HHAbort() }
;

s1: . -> s11
    a -> s1
    b -> s11
    c -> s2
    d -> s0
    f -> s211
;

s11: g -> s211
     h ?(pd.test == 1) -- { pd.test = 0; }
;

s2: . -> s21
    c -> s1
    f -> s11
;

s21: . -> s211
     b -> s211
     h ?(pd.test == 0) -> s21 { pd.test = 1; }
;

s211: d -> s21
      g -> s0
;


%%

myKeypress = Keypress2(getch, MyData())
myKeypress.enableTrace()
myKeypress.run()

# all done!
