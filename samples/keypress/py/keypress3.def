%{
from inthsm import HHAccept, HHAbort

import sys

def getch(pd):
    print('\nEnter [a - h], or [x|X] to exit/abort...\n> ', end='', flush=True);
    while True:
       ch = sys.stdin.read(1)
       if ch != '\n':
           return ch

class MyData:
    def __init__(self):
        self.test = 0

%}

%event	a b c d e f g h
	x X

%state	s0
	s1
	s11
	s2
	s21
	s211

%hiera	s0 (s1 (s11),
	    s2 (s21 (s211)))

%start	s0

%%

s0: . -> s1
    e -> s211
    x --	{ HHAccept() }
    X --	{ HHAbort() }
    * --
;

s1: . -> s11
    a -> s1
    b |> s11	# local transition to sub state
    c -> s2
    d >| s0	# local transition to parent state
    f -> s211
;

s11: g -> s211
     h ?(pd.test == 1) -- { pd.test = 0; }
;

s2: . -> s21
    a <<	# event deferral
    c -> s1
    f -> s11
;

s21: . -> s211
     b |> s211
     h ?(pd.test == 0) -> s21 { pd.test = 1; }
;

s211: d >| s21
      g -> s0
;


%%

myKeypress = Keypress3(getch, MyData())
myKeypress.enableTrace()
myKeypress.run()

# all done!
